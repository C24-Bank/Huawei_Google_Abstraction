plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion androidVersions.compileSdkVersion
    buildToolsVersion androidVersions.buildTools

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation dependenciesList.kotlin
    implementation dependenciesList.androidXcore
    implementation dependenciesList.androidXAppcompat
    implementation dependenciesList.material

    implementation project(':core-pushkit')

    implementation platform(dependenciesList.firebaseBom)
    // Add the dependency for the Firebase SDK for Google Analytics
    // When using the BoM, don't specify versions in Firebase dependencies
    implementation dependenciesList.firebaseMessaging
    implementation dependenciesList.firebaseMessagingDirectBoot

    testImplementation dependenciesList.junit

}

def getVersionName = { ->
    return "${versionProperties.getProperty("cloudMessagingVersion", "0.0.1")}"
}

def getArtificatId = { ->
    return "google-cloudmessaging" // Replace with library name ID
}

afterEvaluate {
    publishing {
        publications {
            bar(MavenPublication) {
                from components.release
                groupId mavenGroupId
                artifactId getArtificatId()
                version getVersionName()
            }
        }
    }
}